
[[qav-analysis-dsl]]
= Analysis DSL

A QAvalidator analysis is composed of these types of operations which can be combined using the QAvalidator Analysis DSL:

1. Read Input
2. Create architecture views on a graph (full graph or filtered graph)
3. Manipulation
4. Validation, according to specified rules
5. Export and Visualization

This chapter gives a detailed definition of the QAvalidator Analysis DSL.

== Example

Before diving in, here is an example of an analysis DSL.

It is commented inline to explain what's going on.
Based on this working example, the reference part for the analysis DSL should be easier to read.

[source,groovy]
-----
import de.qaware.qav.graph.api.DependencyType

apply "de.qaware.qav.analysis.plugins.ShortcutQavPlugin"

// note: here are no inputJava and outuptDir statements. Use this file from within a
// Maven build, and those values will be set automatically.

analysis("Step 1: Analyze Package Architecture") {
    // Prepare input: analyze the graph and create an architecture tree, based on
    // the package hierarchy:
    // packageGraph is now a filtered graph which only contains those nodes which
    // belong to the package architecture (i.e. it does not contain the class nodes).
    def packageGraph = createPackageArchitectureView(inputClassesGraph)

    def packageCycleGraph = findCycles(packageGraph, "Package")

    // output:
    printNodes(packageCycleGraph, "packageCycleNodes.txt")
    writeDot(packageGraph, "packageGraph", architecture("Package"))
}

analysis("Step 2: Prepare T-View Architecture") {
    // Read the given Architecture DSL file. The architecture will be available
    // under the name defined in the Architecture DSL file; it can be accessed
    // with: architecture("T-View")
    readArchitecture "${systemName}_architecture.groovy"

    // Use that architecture and apply it on the dependency graph.
    // Do so twice: once for the full graph, and once only on the part of graph
    // which represents only classes from the input scope (leaving out referenced
    // 3rd-party classes).
    architectureTView = createArchitectureView(allClassesGraph, architecture("T-View"))
    architectureTViewOnInput = createArchitectureView(
                    inputClassesGraph, architecture("T-View"), "T-View-on-Input")
}

analysis("Step 3: Analyze and check for violations") {
    // Check all architecture rules: all relations must be covered in the architecture
    // definition, all components must actually be implemented, and all rules in the
    // architecture file are really used.
    checkArchitectureRules(architectureTView, architecture("T-View"))

    // Find cycles on the component level
    architectureTViewCycleGraph = findCycles(architectureTView, "T-View")
}

analysis("Step 4: Export as DOT, GraphML, and JSON") {
    // graphical export as DOT (for GraphViz) and GraphML (for yEd)
    writeDot(architectureTView, "architectureTView", architecture("T-View"))
    writeDot(architectureTViewCycleGraph, "architectureTViewCycleGraph",
                    architecture("T-View"))
    writeDot(architectureTViewOnInput, "architectureTViewOnInput",
                    architecture("T-View"))

    // this is to import it into qav-server for interactive exploration of the
    // dependency graph
    writeFile(dependencyGraph, "dependencyGraph.json")
}

-----


// Here are the hard-coded parts of the Analysis DSL:
include::41_analysis_dsl_hardcoded.adoc[]

// Here are the plugin documentations:
include::42_analysis_plugins.adoc[]


[[qav-analysis-dsl-fail-build]]
== Failing the Build

If QAvalidator is used via the Maven plugin, it can be configured to break the build if validations fail, or if serious
errors occur.


[TIP]
=====
Technically speaking, this is the case when an Exception is thrown.
This happens for

* detected violations, signalled using the `violation` keyword.
* failed validations like failing `assert` statements in the analysis DSL file,
* I/O errors, or
* other internal errors, like NPEs etc.
=====
